import org.gradle.api.internal.classpath.ModuleRegistry
import org.gradle.api.internal.project.ProjectInternal

buildscript {
  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.gradle.publish:plugin-publish-plugin:0.10.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.16.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply plugin: "com.vanniktech.maven.publish"

gradlePlugin {
  plugins {
    androidJavadocPlugin {
      id = 'com.vanniktech.android.javadoc'
      implementationClass = 'com.vanniktech.android.javadoc.Generation'
    }
  }
}

repositories {
  mavenCentral()
  google()
  jcenter()
}

dependencies {
  api gradleApi()
  api localGroovy()

  compileOnly 'com.android.tools.build:gradle:4.2.1'

  testImplementation 'com.android.tools.build:gradle:4.2.1'
  testImplementation 'junit:junit:4.12'

  // https://github.com/gradle/gradle/issues/16774#issuecomment-893493869
  def toolingApiBuildersJar = (project as ProjectInternal).services.get(ModuleRegistry.class)
    .getModule("gradle-tooling-api-builders")
    .classpath
    .asFiles
    .first()
  testRuntimeOnly(files(toolingApiBuildersJar))
}

sourceCompatibility = JavaVersion.VERSION_1_8

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    androidJavadocPlugin {
      displayName = POM_NAME
      tags = ['gradle', 'android', 'javadoc', 'app module', 'library module']
      description = POM_DESCRIPTION
    }
  }
}

tasks.withType(Test) {
  testLogging {
    testLogging.exceptionFormat = 'full'
  }
}

wrapper {
  gradleVersion = '6.8.3'
  distributionType = Wrapper.DistributionType.ALL
}
