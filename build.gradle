buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.8.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.vanniktech.android.junit.jacoco'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:2.3.3'

    testCompile 'junit:junit:4.12'
}

sourceCompatibility = JavaVersion.VERSION_1_7

group = GROUP
version = VERSION_NAME

pluginBundle {
    website = POM_URL
    vcsUrl = POM_SCM_URL

    plugins {
        androidJavadoc {
            id = 'com.vanniktech.android.javadoc'
            displayName = POM_NAME
            tags = ['gradle', 'android', 'javadoc', 'app module', 'library module']
            description = POM_DESCRIPTION
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionType = Wrapper.DistributionType.ALL
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from 'build/docs/groovydoc'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives groovydocJar
    archives javadocJar
    archives sourcesJar
}

test {
    environment "ANDROID_HOME", System.getenv("ANDROID_HOME")
    testLogging {
        showStackTraces true
        showStandardStreams = true
        events "failed"
        exceptionFormat "full"
    }
}

apply from: file('gradle/gradle-mvn-push.gradle')